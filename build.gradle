plugins {
  id 'application'
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id "com.github.ben-manes.versions" version "$versionsVersion"
}

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
  mavenLocal()
  mavenCentral()
}

sourceCompatibility = 11

dependencies {

  // Misc
  api "args4j:args4j:$argsVersion"
  api "ch.qos.logback:logback-classic:$logbackVersion"
  api 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
  api "com.foilen:jl-smalltools-main:$smalltoolsVersion"
  api "com.google.guava:guava:31.1-jre"
  api 'org.apache.commons:commons-lang3:3.12.0'
  api 'org.slf4j:slf4j-api:2.0.6'
  
  // Databases
  api "org.mariadb.jdbc:mariadb-java-client:$dbMariadbVersion"
  api "org.mongodb:mongodb-driver-sync:$dbMongodbVersion"
  
  // Spring
  api "org.springframework.data:spring-data-jdbc:$springDataJdbcVersion"
  
  // Test
  testCompileOnly 'junit:junit:4.13.2'
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junitVersion"
  testImplementation "org.junit.platform:junit-platform-commons:$junitPlatformVersion"
  testImplementation "org.junit.platform:junit-platform-engine:$junitPlatformVersion"
}

test {
  useJUnitPlatform()
}

java {
  withJavadocJar()
  withSourcesJar()
}

application {
  mainClass.set('com.foilen.databasetools.Application')
  applicationDefaultJvmArgs = ['-Xmx200m']
}

publishing {
  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = System.getenv('OSSRH_USER')
        password = System.getenv('OSSRH_PASS')
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      pom {
        name = "database-tools"
        description = 'This is an application to help manipulating databases'
        url = 'https://github.com/foilen/database-tools'
        from components.java

        scm {
          connection = "scm:git:git@github.com:foilen/database-tools.git"
          url = "https://github.com/foilen/database-tools"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        developers {
          developer {
            id = 'foilen'
            name = 'Simon Levesque'
            email = 'simon@foilen.com'
          }
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}
